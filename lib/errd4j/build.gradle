apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'jdepend'
// TODO checkstyle

def customSrc = 'java_src'
mainClassName = 'io.sysmo.errd4j.Errd4j'

sourceSets {
    main {
        java {
            srcDir customSrc + '/main/java'
        }
        resources {
            srcDir customSrc + '/main/ressources'
        }
    }
    test {
        java {
            srcDir customSrc + '/test/java'
        }
        resources {
            srcDir customSrc + '/test/ressources'
        }
    }
}

repositories {
    // get latest rrd4j from here
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }

    // all other dependencies from maven central (including jinterface)
    mavenCentral()
}

dependencies {
    compile group: 'org.rrd4j',      name: 'rrd4j',      version: '2.3-SNAPSHOT'
    compile group: 'org.erlang.otp', name: 'jinterface', version: '1.5.6'
}


jdepend {
    ignoreFailures = true
    sourceSets = [sourceSets.main]
}

findbugs {
    ignoreFailures = true
    sourceSets = [sourceSets.main]
}

pmd {
    ignoreFailures = false
    sourceSets = [sourceSets.main]
}

applicationDistribution.from(customSrc + '/dist') {
    into ''
}
