apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'pmd'
apply plugin: 'findbugs'
apply plugin: 'jdepend'
// TODO checkstyle

def customSrc = 'java_src'
mainClassName = 'io.sysmo.equartz.EQuartz'

sourceSets {
    main {
        java {
            srcDir customSrc + '/main/java'
        }
        resources {
            srcDir customSrc + '/main/ressources'
        }
    }
    test {
        java {
            srcDir customSrc + '/test/java'
        }
        resources {
            srcDir customSrc + '/test/ressources'
        }
    }
}

repositories {
    // all dependencies from maven central (including jinterface)
    mavenCentral()
}

dependencies {
    compile group: 'org.quartz-scheduler', name: 'quartz', version: '2.2.1'
    compile group: 'org.erlang.otp', name: 'jinterface', version: '1.5.6'
}

jdepend {
    ignoreFailures = false
    sourceSets = [sourceSets.main]
}

findbugs {
    ignoreFailures = true
    sourceSets = [sourceSets.main]
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

pmd {
    ignoreFailures = false
    sourceSets = [sourceSets.main]
}

applicationDistribution.from(customSrc + '/dist') {
    into ''
}
