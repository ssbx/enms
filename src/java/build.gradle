
subprojects {
    apply plugin: 'java'
    apply plugin: 'pmd'
    apply plugin: 'findbugs'
    apply plugin: 'jdepend'

    sourceCompatibility = 1.7

    repositories {
        mavenCentral()

        // rrd4j
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }

        // snmp4j
        maven {
            url 'https://oosnmp.net/dist/release'
        }

    }

    dependencies {
        testCompile 'junit:junit:4.2'
    }

    jdepend  { ignoreFailures = false }
    findbugs { ignoreFailures = false }
    pmd      { ignoreFailures = false }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }
}

project(':shared:nchecks') {
    dependencies {
        compile 'org.erlang.otp:jinterface:1.5.6'
        compile 'org.glassfish:javax.json:1.0.4'
        compile 'org.jruby:jruby:1.7.20'
        compile 'org.snmp4j:snmp4j:2.3.3'
        compile 'org.slf4j:slf4j-api:1.7.12'
        compile 'org.slf4j:slf4j-jdk14:1.7.12'
    }
}

project(':sysmo-jserver') {
    apply plugin: 'application'

    mainClassName = 'io.sysmo.jserver.SysmoServer'
    applicationDefaultJvmArgs = [
            "-server",
            "-Duser.language=en", "-Duser.country=US",
            "-Djava.util.logging.config.file=etc/sysmo-log.properties"]

    dependencies {
        compile project(':shared:nchecks')
        compile 'org.erlang.otp:jinterface:1.5.6'
        compile 'org.rrd4j:rrd4j:3.0-SNAPSHOT'
        compile 'org.snmp4j:snmp4j:2.3.3'
        compile 'org.slf4j:slf4j-api:1.7.12'
        compile 'org.slf4j:slf4j-jdk14:1.7.12'
        compile 'org.eclipse.jetty:jetty-server:8.1.17.v20150415'
        compile 'org.apache.derby:derby:10.11.1.1'
    }
}

project(':sysmo-jworker') {
    apply plugin: 'application'

    mainClassName = 'io.sysmo.jworker.SysmoWorker'
    applicationDefaultJvmArgs = [
            "-server",
            "-Duser.language=en", "-Duser.country=US"]
            //"-Djava.util.logging.config.file=etc/sysmo-log.properties"]

    dependencies {
        compile project(':shared:nchecks')
        compile 'org.erlang.otp:jinterface:1.5.6'
        compile 'org.slf4j:slf4j-api:1.7.12'
        compile 'org.slf4j:slf4j-jdk14:1.7.12'
    }
}
